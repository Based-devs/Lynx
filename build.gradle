buildscript {
    repositories {
        maven {
            url = 'https://files.minecraftforge.net/maven'
        }

        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = '0.1'
group = 'dev.based.vampyrix'
archivesBaseName = 'vampyrix'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'
    mappings = 'stable_39'
    makeObfSourceJar = false
    clientJvmArgs += '-Dfml.coreMods.load=dev.based.vampyrix.asm.VampyrixMixinLoader'
    clientRunArgs += '--mixin mixins.vampyrix.json'
}

repositories {
    mavenCentral()

    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }

    compile group: 'org.json', name: 'json', version: '20211205'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    defaultObfuscationEnv searge

    sourceSets {
        main {
            ext.refMap = 'mixins.vampyrix.refmap.json'
        }
    }
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }

    manifest {
        attributes(
                'MixinConfigs': 'mixins.vampyrix.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'dev.based.vampyrix.asm.VampyrixMixinLoader',
                'ForceLoadAsMod': 'true'
        )
    }
    classifier = 'release'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)
